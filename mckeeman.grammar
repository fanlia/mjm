
grammar
    rules

space
    '0020'

newline
    '000A'

name
    letter
    letter name

letter
    'a' . 'z'
    'A' . 'Z'
    '_'

indentation
    space space space space

rules
    rule
    rule newline rules

rule
    name newline nothing alternatives

nothing
    ""
    indentation '"' '"' newline

alternatives
    alternative
    alternative alternatives

alternative
    indentation items newline

items
    item
    item space items

item
    literal
    name

literal
    range exclude
    singleton
    '"' characters '"'

singleton
    ''' codepoint '''

codepoint
    hexcode
    ' ' . '10FFFF'

hexcode
    "10" hex hex hex hex
    hex hex hex hex hex
    hex hex hex hex

hex
    '0' . '9'
    'A' . 'F'

range
    singleton space '.' space singleton

exclude
    ""
    space '-' space range exclude
    space '-' space singleton exclude

characters
    character
    character characters

character
    ' ' . '10FFFF' - '"'

